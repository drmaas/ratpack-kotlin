plugins {
  id "org.jetbrains.kotlin.jvm" version "1.4.32"
  id "com.github.ben-manes.versions" version "0.36.0"
  id "nebula.release" version "15.3.1"
  id "com.vanniktech.dependency.graph.generator" version "0.5.0"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

repositories {
  mavenCentral()
}

group= "me.drmaas"

subprojects {
  apply plugin: "idea"
  apply plugin: "kotlin"
  apply plugin: "java-library"
  apply plugin: "maven-publish"

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation platform("org.jetbrains.kotlin:kotlin-bom:1.4.32")
    implementation platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.4.3")
  }

  test {
    useJUnitPlatform()
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(8)
    }
  }

  publishing {
    repositories {
      maven {
        name = "OSSRH"
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username = System.getenv("MAVEN_USERNAME")
          password = System.getenv("MAVEN_PASSWORD")
        }
      }
    }
    publications {
      mavenPub(MavenPublication) {
        from components.java

        pom {
          name = 'Ratpack kotlin dsl library'
          description = 'Ratpack kotlin dsl library'
          url = 'https://github.com/drmaas/ratpack-kotlin'
          inceptionYear = '2016'

          scm {
            url = 'https://github.com/drmaas/ratpack-kotlin'
            connection = 'scm:https://drmaas@github.com/drmaas/ratpack-kotlin.git'
            developerConnection = 'scm:git://github.com/drmaas/ratpack-kotlin.git'
          }

          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          developers {
            developer {
              id = 'drmaas'
              name = 'Dan Maas'
              email = 'drmaas@gmail.com'
            }
          }
        }
      }
    }
  }

}

nexusPublishing {
  repositories {
    // set credentials as sonatypeUsername and sonatypePassword project properties,
    // e.g. in ~/.gradle/gradle.properties
    sonatype()
  }
}

apply from: 'dependencies.gradle'
