buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21'
    classpath 'com.netflix.nebula:nebula-release-plugin:11.1.0'
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
  }
}

apply plugin: 'nebula.nebula-release'
apply plugin: 'io.codearte.nexus-staging'

allprojects {
  group = 'me.drmaas'
  apply plugin: "java-library"
  apply plugin: "idea"

  java.sourceCompatibility = JavaVersion.VERSION_11
  java.targetCompatibility = JavaVersion.VERSION_11
}

nebulaRelease {
  releaseBranchPatterns = [/master/, /HEAD/, /main/, /(release(-|\/))?\d+(\.\d+)?\.x/, /v?\d+\.\d+\.\d+/]
}

subprojects {
  apply plugin: "org.jetbrains.kotlin.jvm"
  apply plugin: "com.bmuschko.nexus"
  apply plugin: "maven-publish"

  repositories {
    mavenLocal()
    mavenCentral()
  }

  test {
    useJUnitPlatform()
    testLogging {
      showStandardStreams = false
    }
    beforeSuite {}
    afterSuite {}
    beforeTest { desc ->
      logger.lifecycle("START $desc.className > ${desc.name.replaceAll('\\(.*?\\)\\z', '')}")
    }
    afterTest { desc, result ->
      logger.lifecycle(
        String.format("%s (%4dms) %s > %s",
          result.resultType,
          result.endTime - result.startTime,
          desc.className,
          // throw away the trailing (.*) at the end of the method name
          desc.name.replaceAll("\\(.*?\\)\\z", "")
        )
      )
    }
  }

  modifyPom {
    project {
      name 'Ratpack kotlin dsl library'
      description 'Ratpack kotlin dsl library'
      url 'https://github.com/drmaas/ratpack-kotlin'
      inceptionYear '2016'

      scm {
        url 'https://github.com/drmaas/ratpack-kotlin'
        connection 'scm:https://drmaas@github.com/drmaas/ratpack-kotlin.git'
        developerConnection 'scm:git://github.com/drmaas/ratpack-kotlin.git'
      }

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'drmaas'
          name 'Dan Maas'
          email 'drmaas@gmail.com'
        }
      }
    }
  }
}

apply from: 'dependencies.gradle'
